plugins {
    id 'java'
    id 'maven-publish'
}

group = 'uz.sb'
version = '1.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'tst '
                description = 'This is core for our bank system project. It contains all the core logic and entities.'
                url = 'https://github.com/SardorBoboyev/LibraryForChat'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'SardorBoboyev'
                        name = 'SardorBoboyev'
                        email = 'boboyevsardor350@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/SardorBoboyev/LibraryForChat.git'
                    developerConnection = 'scm:git:ssh://github.com/SardorBoboyev/LibraryForChat.git'
                    url = 'https://github.com/SardorBoboyev/LibraryForChat'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/SardorBoboyev/LibraryForChat")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER")
                password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    implementation 'org.springframework.boot:spring-boot-starter-validation:3.3.4'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}
